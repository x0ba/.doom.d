#+title: Doom Emacs Configuration
#+author: x0ba

* Intro
** Why Emacs?
I've been through a lot of editors over time. Neovim, VSCode, Helix, even Zed for a
short period of time. But I always find myself coming back to Emacs. Maybe it's
because of org-mode. Maybe it's because you can literally make emacs your operating
system. But I think the main reason is that Emacs is unparallelled in /extensibility/.
Emacs isn't an editor. It's a lisp interpreter that just happens to include an editor.
And tetris. That's important.

* Configuration
** Personal info
Time to give doom my name.
#+begin_src emacs-lisp
(setq user-full-name "Daniel Xu"
      user-mail-address "x0ba@tuta.io")
#+end_src
** Scratch
 When I bring up Doom's scratch buffer with SPC x, it's often to play with
 elisp or note something down (that isn't worth an entry in my notes). I can
 do both in =lisp-interaction-mode=.
 #+begin_src emacs-lisp
(setq doom-scratch-initial-major-mode 'lisp-interaction-mode)
 #+end_src
 
** UI
*** Solaire
#+begin_src emacs-lisp
(use-package solaire-mode
  :hook (after-init . solaire-global-mode)
  :config
  (push '(treemacs-window-background-face . solaire-default-face) solaire-mode-remap-alist)
  (push '(treemacs-hl-line-face . solaire-hl-line-face) solaire-mode-remap-alist))
#+end_src
*** Theme
Let's set our theme to my customized version of catppuccin
#+begin_src emacs-lisp
(setq doom-theme 'doom-mountain)
#+end_src
*** Fonts
Let's set our font to Liga Berkeley Mono for monospace and IBM Plex Sans for variable-pitch:
#+begin_src emacs-lisp
(setq doom-font (font-spec :family "Liga Berkeley Mono" :size 14)
      doom-big-font (font-spec :family "Liga Berkeley Mono" :size 26)
      doom-variable-pitch-font (font-spec :family "IBM Plex Sans" :size 15 :weight 'medium)
      doom-unicode-font (font-spec :family "JuliaMono")
      doom-serif-font (font-spec :family "IBM Plex Serif" :size 14 :weight 'light))
#+end_src
*** Modeline
Let's add some space to the modeline. 35 makes it feel a bit comfier
#+begin_src emacs-lisp
(setq doom-modeline-height 35)
#+end_src
*** Line Height
Let's make Emacs feel less cramped.
#+begin_src emacs-lisp
(setq-default line-spacing 0.20)
#+end_src

** Better Defaults
Emacs' defaults aren't so good nowadays. Let's fix that:
#+begin_src emacs-lisp
(setq scroll-margin 2
      auto-save-default t
      display-line-numbers-type 'relative
      delete-by-moving-to-trash t
      truncate-string-ellipsis "â€¦")
(global-subword-mode 1)
(setq global-hl-line-modes nil)
#+end_src
** Golden Ratio
By default,  Emacs splits are not the right size for editing. Golden ratio
automatically resizes emacs to the perfect (one might say golden) ratio.
#+begin_src emacs-lisp
(use-package! golden-ratio
  :hook (after-init . golden-ratio-mode)
  :custom
  (golden-ratio-auto-scale t)
  (golden-ratio-exclude-modes '(treemacs-mode occur-mode))
  :config
  (mapc (lambda (cmd) (add-to-list 'golden-ratio-extra-commands cmd))
      '(ace-window
        evil-window-delete
        evil-window-split
        evil-window-vsplit
        evil-window-left
        evil-window-right
        evil-window-up
        evil-window-down
        evil-window-bottom-right
        evil-window-top-left
        evil-window-mru
        evil-window-next
        evil-window-prev
        evil-window-new
        evil-window-vnew
        evil-window-rotate-upwards
        evil-window-rotate-downwards
        evil-window-move-very-top
        evil-window-move-far-left
        evil-window-move-far-right
        evil-window-move-very-bottom)))
#+end_src
** Vertico Posframe
Instead of the ugly-looking vertico window on the bottom,
let us show vertico in a nice posframe in the middle
#+begin_src emacs-lisp
(use-package vertico-posframe
  :hook (after-init . vertico-posframe-mode)
  :custom
  (vertico-posframe-parameters
   '((left-fringe . 8)
     (right-fringe . 8))))
#+end_src
** Mixed-pitch
Weâ€™d like to use mixed pitch in certain modes. If we simply add a hook,
when directly opening a file with (a new) Emacs mixed-pitch-mode runs
before UI initialisation, which is problematic. To resolve this, we create
a hook that runs after UI initialisation and both

+ conditionally enables mixed-pitch-mode
+ sets up the mixed pitch hooks

#+begin_src emacs-lisp
(defvar mixed-pitch-modes '(org-mode LaTeX-mode markdown-mode gfm-mode Info-mode)
  "Modes that `mixed-pitch-mode' should be enabled in, but only after UI initialisation.")
(defun init-mixed-pitch-h ()
  "Hook `mixed-pitch-mode' into each mode in `mixed-pitch-modes'.
Also immediately enables `mixed-pitch-modes' if currently in one of the modes."
  (when (memq major-mode mixed-pitch-modes)
    (mixed-pitch-mode 1))
  (dolist (hook mixed-pitch-modes)
    (add-hook (intern (concat (symbol-name hook) "-hook")) #'mixed-pitch-mode)))
(add-hook 'doom-init-ui-hook #'init-mixed-pitch-h)
(autoload #'mixed-pitch-serif-mode "mixed-pitch"
  "Change the default face of the current buffer to a serifed variable pitch, while keeping some faces fixed pitch." t)

(setq! variable-pitch-serif-font (font-spec :family "Alegreya" :size 17))

(after! mixed-pitch
  (setq mixed-pitch-set-height t)
  (set-face-attribute 'variable-pitch-serif nil :font variable-pitch-serif-font)
  (defun mixed-pitch-serif-mode (&optional arg)
    "Change the default face of the current buffer to a serifed variable pitch, while keeping some faces fixed pitch."
    (interactive)
    (let ((mixed-pitch-face 'variable-pitch-serif))
      (mixed-pitch-mode (or arg 'toggle)))))



(set-char-table-range composition-function-table ?f '(["\\(?:ff?[fijlt]\\)" 0 font-shape-gstring]))
(set-char-table-range composition-function-table ?T '(["\\(?:Th\\)" 0 font-shape-gstring]))

(defface variable-pitch-serif
    '((t (:family "serif")))
    "A variable-pitch face with serifs."
    :group 'basic-faces)

(defcustom variable-pitch-serif-font (font-spec :family "serif")
  "The font face used for `variable-pitch-serif'."
  :group 'basic-faces
  :set (lambda (symbol value)
         (set-face-attribute 'variable-pitch-serif nil :font value)
         (set-default-toplevel-value symbol value)))
#+end_src

** Marginalia
#+begin_src emacs-lisp
(after! marginalia
  (setq marginalia-censor-variables nil)

  (defadvice! +marginalia--anotate-local-file-colorful (cand)
    "Just a more colourful version of `marginalia--anotate-local-file'."
    :override #'marginalia--annotate-local-file
    (when-let (attrs (file-attributes (substitute-in-file-name
                                       (marginalia--full-candidate cand))
                                      'integer))
      (marginalia--fields
       ((marginalia--file-owner attrs)
        :width 12 :face 'marginalia-file-owner)
       ((marginalia--file-modes attrs))
       ((+marginalia-file-size-colorful (file-attribute-size attrs))
        :width 7)
       ((+marginalia--time-colorful (file-attribute-modification-time attrs))
        :width 12))))

  (defun +marginalia--time-colorful (time)
    (let* ((seconds (float-time (time-subtract (current-time) time)))
           (color (doom-blend
                   (face-attribute 'marginalia-date :foreground nil t)
                   (face-attribute 'marginalia-documentation :foreground nil t)
                   (/ 1.0 (log (+ 3 (/ (+ 1 seconds) 345600.0)))))))
      ;; 1 - log(3 + 1/(days + 1)) % grey
      (propertize (marginalia--time time) 'face (list :foreground color))))

  (defun +marginalia-file-size-colorful (size)
    (let* ((size-index (/ (log10 (+ 1 size)) 7.0))
           (color (if (< size-index 10000000) ; 10m
                      (doom-blend 'orange 'green size-index)
                    (doom-blend 'red 'orange (- size-index 1)))))
      (propertize (file-size-human-readable size) 'face (list :foreground color)))))
#+end_src
** Avy
The best package ever
#+begin_src emacs-lisp
(use-package! avy
  :config
  (setq avy-all-windows t))
#+end_src
** MacOS
Some Mac specific tweaks

Make the titlebar smaller, it's huge by default
This is slightly janky, but since Doom loads the toolbar lazily this
is needed.
#+begin_src emacs-lisp
(add-hook 'doom-after-init-hook (lambda () (tool-bar-mode 1) (tool-bar-mode 0)))
(use-package! ns-auto-titlebar
  :config
  (when (eq system-type 'darwin) (ns-auto-titlebar-mode)))
#+end_src
** Zen-mode
Zen-mode zooms in a bit too much by default
#+begin_src emacs-lisp
(setq +zen-text-scale 0.8)
#+end_src
#+begin_src emacs-lisp
(defvar +zen-serif-p t
  "Whether to use a serifed font with `mixed-pitch-mode'.")
(defvar +zen-org-starhide t
  "The value `org-modern-hide-stars' is set to.")

(after! writeroom-mode
  (defvar-local +zen--original-org-indent-mode-p nil)
  (defvar-local +zen--original-mixed-pitch-mode-p nil)
  (defun +zen-enable-mixed-pitch-mode-h ()
    "Enable `mixed-pitch-mode' when in `+zen-mixed-pitch-modes'."
    (when (apply #'derived-mode-p +zen-mixed-pitch-modes)
      (if writeroom-mode
          (progn
            (setq +zen--original-mixed-pitch-mode-p mixed-pitch-mode)
            (funcall (if +zen-serif-p #'mixed-pitch-serif-mode #'mixed-pitch-mode) 1))
        (funcall #'mixed-pitch-mode (if +zen--original-mixed-pitch-mode-p 1 -1)))))
  (defun +zen-prose-org-h ()
    "Reformat the current Org buffer appearance for prose."
    (when (eq major-mode 'org-mode)
      (setq display-line-numbers nil
            visual-fill-column-width 60
            org-adapt-indentation nil)
      (when (featurep 'org-modern)
        (setq-local org-modern-star '("ðŸ™˜" "ðŸ™™" "ðŸ™š" "ðŸ™›")
                    ;; org-modern-star '("ðŸ™" "ðŸ™‘" "ðŸ™’" "ðŸ™“" "ðŸ™”" "ðŸ™•" "ðŸ™–" "ðŸ™—")
                    org-modern-hide-stars +zen-org-starhide)
        (org-modern-mode -1)
        (org-modern-mode 1))
      (setq
       +zen--original-org-indent-mode-p org-indent-mode)
      (org-indent-mode -1)))
  (defun +zen-nonprose-org-h ()
    "Reverse the effect of `+zen-prose-org'."
    (when (eq major-mode 'org-mode)
      (when (bound-and-true-p org-modern-mode)
        (org-modern-mode -1)
        (org-modern-mode 1))
      (when +zen--original-org-indent-mode-p (org-indent-mode 1))))
  (pushnew! writeroom--local-variables
            'display-line-numbers
            'visual-fill-column-width
            'org-adapt-indentation
            'org-modern-mode
            'org-modern-star
            'org-modern-hide-stars)
  (add-hook 'writeroom-mode-enable-hook #'+zen-prose-org-h)
  (add-hook 'writeroom-mode-disable-hook #'+zen-nonprose-org-h))
#+end_src
** Evil
Focus the new window after splitting
#+begin_src emacs-lisp
(setq evil-split-window-below t
      evil-vsplit-window-right t)
#+end_src
Implicit /g flag on evil ex substitution, because I use the default behavior
less often.
#+begin_src emacs-lisp
(setq evil-ex-substitute-global t)
#+end_src
** Dashboard
Let's set a fancy splash image and remove all the useless stuff on the
=doom-dasboard=
#+begin_src emacs-lisp
(setq fancy-splash-image (file-name-concat doom-user-dir "splash.png"))
(setq +doom-dashboard-functions '(doom-dashboard-widget-banner))
#+end_src
** Spacious Padding Mode
adds padding around windows and frames
#+begin_src emacs-lisp
(use-package spacious-padding
  :ensure t
  :defer
  :hook (after-init . spacious-padding-mode))
#+end_src
** Org Mode
*** Directories
I like to have my org files in =~/org= and to add my =school.org= and =todo.org=
files to the agenda.
#+begin_src emacs-lisp
(setq org-directory "~/org/")
(after! org-agenda
  (setq org-agenda-files (list "~/org/agenda.org"
                               "~/org/todo.org")))
#+end_src

*** Modern
#+begin_src emacs-lisp
(use-package! org-modern
  :hook (org-mode . org-modern-mode)
  :config
  (setq org-modern-star '("â—‰" "â—‹" "âœ¸" "âœ¿" "âœ¤" "âœœ" "â—†" "â–¶")
        org-modern-table-vertical 1
        org-modern-table-horizontal 0.2
        org-modern-list '((43 . "âž¤")
                          (45 . "â€“")
                          (42 . "â€¢"))
        org-modern-todo-faces
        '(("TODO" :inverse-video t :inherit org-todo)
          ("PROJ" :inverse-video t :inherit +org-todo-project)
          ("STRT" :inverse-video t :inherit +org-todo-active)
          ("[-]"  :inverse-video t :inherit +org-todo-active)
          ("HOLD" :inverse-video t :inherit +org-todo-onhold)
          ("WAIT" :inverse-video t :inherit +org-todo-onhold)
          ("[?]"  :inverse-video t :inherit +org-todo-onhold)
          ("KILL" :inverse-video t :inherit +org-todo-cancel)
          ("NO"   :inverse-video t :inherit +org-todo-cancel))
        org-modern-footnote
        (cons nil (cadr org-script-display))
        org-modern-block-fringe nil
        org-modern-block-name
        '((t . t)
          ("src" "Â»" "Â«")
          ("example" "Â»â€“" "â€“Â«")
          ("quote" "â" "âž")
          ("export" "â©" "âª"))
        org-modern-progress nil
        org-modern-priority nil
        org-modern-horizontal-rule (make-string 36 ?â”€)
        org-modern-keyword
        '((t . t)
          ("title" . "ð™")
          ("subtitle" . "ð™©")
          ("author" . "ð˜¼")
          ("email" . #("îƒ¡" 0 1 (display (raise -0.14))))
          ("date" . "ð˜¿")
          ("property" . "â˜¸")
          ("options" . "âŒ¥")
          ("startup" . "â»")
          ("macro" . "ð“œ")
          ("bind" . #("î…—" 0 1 (display (raise -0.1))))
          ("bibliography" . "ï…")
          ("print_bibliography" . #("î•‹" 0 1 (display (raise -0.1))))
          ("cite_export" . "ï…â®­")
          ("print_glossary" . #("î•‹á´¬á¶»" 0 1 (display (raise -0.1))))
          ("glossary_sources" . #("î“" 0 1 (display (raise -0.14))))
          ("include" . "â‡¤")
          ("setupfile" . "â‡š")
          ("html_head" . "ðŸ…·")
          ("html" . "ðŸ…—")
          ("latex_class" . "ðŸ„»")
          ("latex_class_options" . #("ðŸ„»î¢¸" 1 2 (display (raise -0.14))))
          ("latex_header" . "ðŸ…»")
          ("latex_header_extra" . "ðŸ…»âº")
          ("latex" . "ðŸ…›")
          ("beamer_theme" . "ðŸ„±")
          ("beamer_color_theme" . #("ðŸ„±îŠ" 1 2 (display (raise -0.12))))
          ("beamer_font_theme" . "ðŸ„±ð€")
          ("beamer_header" . "ðŸ…±")
          ("beamer" . "ðŸ…‘")
          ("attr_latex" . "ðŸ„›")
          ("attr_html" . "ðŸ„—")
          ("attr_org" . "â’ª")
          ("call" . #("î•¦" 0 1 (display (raise -0.15))))
          ("name" . "â")
          ("header" . "â€º")
          ("caption" . "â˜°")
          ("results" . "ðŸ ¶")))
  (custom-set-faces! '(org-modern-statistics :inherit org-checkbox-statistics-todo)))
#+end_src

*** Font Display
Mixed-pitch is great. As is =+org-pretty-mode=, let's use them
#+begin_src emacs-lisp
(add-hook 'org-mode-hook #'+org-pretty-mode)
#+end_src
Let's make headings a bit bigger
#+begin_src emacs-lisp
(custom-set-faces!
  '(outline-1 :weight extra-bold :height 1.25)
  '(outline-2 :weight bold :height 1.15)
  '(outline-3 :weight bold :height 1.12)
  '(outline-4 :weight semi-bold :height 1.09)
  '(outline-5 :weight semi-bold :height 1.06)
  '(outline-6 :weight semi-bold :height 1.03)
  '(outline-8 :weight semi-bold)
  '(outline-9 :weight semi-bold))
#+end_src
And same with the title
#+begin_src emacs-lisp
(custom-set-faces!
  '(org-document-title :height 1.2))
#+end_src
Org files can be rather nice to look at, particularly with some of the customisations here.
This comes at a cost however, expensive font-lock. Feeling like youâ€™re typing through
molasses in large files is no fun, but there is a way I can defer font-locking when typing
to make the experience more responsive.
#+begin_src emacs-lisp
(defun locally-defer-font-lock ()
  "Set jit-lock defer and stealth, when buffer is over a certain size."
  (when (> (buffer-size) 50000)
    (setq-local jit-lock-defer-time 0.05
                jit-lock-stealth-time 1)))

(add-hook 'org-mode-hook #'locally-defer-font-lock)
#+end_src

*** Symbols
Itâ€™s also nice to change the character used for collapsed items (by default â€¦), I think â–¾ is
better for indicating â€™collapsed sectionâ€™. and add an extra org-bullet to the default list of four.
#+begin_src emacs-lisp
(setq org-ellipsis " â–¾ "
      org-hide-leading-stars t
      org-priority-highest ?A
      org-priority-lowest ?E
      org-priority-faces
      '((?A . 'nerd-icons-red)
        (?B . 'nerd-icons-orange)
        (?C . 'nerd-icons-yellow)
        (?D . 'nerd-icons-green)
        (?E . 'nerd-icons-blue)))
#+end_src

*** Emphasis Markers
While =org-hide-emphasis-markers= is very nice, it can sometimes make
edits which occur at the border a bit more fiddley. We can improve
this situation without sacrificing visual amenities with the =org-appear= package.
#+begin_src emacs-lisp
(use-package! org-appear
  :hook (org-mode . org-appear-mode)
  :config
  (setq org-appear-autoemphasis t
        org-appear-autosubmarkers t
        org-appear-autolinks nil)
  ;; for proper first-time setup, `org-appear--set-elements'
  ;; needs to be run after other hooks have acted.
  (run-at-time nil nil #'org-appear--set-elements))
#+end_src
*** Bullets
I think it makes sense to have list bullets change with
depth
#+begin_src emacs-lisp
(setq org-list-demote-modify-bullet '(("+" . "-") ("-" . "+") ("*" . "+") ("1." . "a.")))
#+end_src

** Keybindings
I prefer ace-window for window switching
#+begin_src emacs-lisp
(global-set-key (kbd "M-p") 'ace-window)
#+end_src
